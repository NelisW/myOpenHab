# MQTT on ESP8266



1.  Set up a broker on a server (not the ESP8266).  In my case this broker is on a Raspberry Pi, running a MQTT 3.1 broker (the latest version is 3.1.1, but my Wheezy OS does not yet include this).

1.  Open the Arduino IDE and install the ESP 8266 board.

1. Install the pubsubclient library (http://pubsubclient.knolleary.net/, https://github.com/knolleary/pubsubclient) in the Arduino IDE. Use the IDE <sketch><include libraries><Manage libraries...>, search for the pubsubclient and install.

2.  Locate the `PubSubClient.h` file and select/comment out the MQTT version you will be using (3.1.1 or 3.1) --- it seems that they are not compatible.  On my PC the file is located here `---\Arduino-IDE\projects\libraries\PubSubClient\src`.
See here:
https://github.com/knolleary/pubsubclient/blob/master/src/PubSubClient.h#L15

1. Paste the following code into the ino file and download.

```Python
//remember to set the MQTT version in the PubSubClient.h file.
//U:\DropboxPortableAHK\Dropbox\IoT\Arduino-IDE\projects\libraries\PubSubClient\src

#include <ESP8266WiFi.h>
#include <PubSubClient.h>

#define wifi_ssid "your-ssid"
#define wifi_password "your-network-password"

#define mqtt_server "your-mqtt-broker-IP"
#define mqtt_user ""
#define mqtt_password ""

WiFiClient espClient;
PubSubClient client(espClient);

void setup_wifi() {
  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(wifi_ssid);

  WiFi.begin(wifi_ssid, wifi_password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    // Attempt to connect
    // If you do not want to use a username and password, change next line to
    if (client.connect("ESP8266Client")) {
    ///if (client.connect("ESP8266Client", mqtt_user, mqtt_password)) {
      Serial.println("connected");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}

void setup() {
  Serial.begin(115200);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  }

long lastMsg = 0;


void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  long now = millis();
  if (now - lastMsg > 1000) {
    lastMsg = now;
    client.publish("testing/photocell", "yes", true);
  }
}
```



References:

https://home-assistant.io/blog/2015/10/11/measure-temperature-with-esp8266-and-report-to-mqtt/

https://github.com/knolleary/pubsubclient/issues/111

https://gist.github.com/igrr/7f7e7973366fc01d6393

installing mosquitto from source:  
http://blog.thingstud.io/recipes/how-to-make-your-raspberry-pi-the-ultimate-iot-hub/

http://www.penninkhof.com/2015/05/linking-the-esp8266-a-raspberry-pi-through-mqtt/
